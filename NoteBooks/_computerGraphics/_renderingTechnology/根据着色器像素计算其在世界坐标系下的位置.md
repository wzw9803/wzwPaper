# 根据着色器像素计算其在世界坐标系下的位置

Vertex Shader

```
precision mediump float;
precision mediump int;

uniform mat4 modelMatrix;
uniform mat4 modelViewMatrix; // optional
uniform mat4 projectionMatrix; // optional

attribute vec3 position;

varying vec3 mPosition;

void main()	{

    mPosition = (modelMatrix * vec4( position, 1.0 )).xyz;

    gl_Position = projectionMatrix * modelViewMatrix * vec4( position, 1.0 );

}
```

Fragment Shader

```
precision mediump float;
precision mediump int;

// 投影矩阵的逆矩阵
uniform mat4 projectionMatrixInverse;
// 视口变换矩阵的逆矩阵
uniform mat4 viewPortMatrixInverse;
// 视图矩阵的逆矩阵
uniform mat4 viewMatrixInverse;

//开始测试的标志
uniform bool testFlag;

varying vec3 mPosition;

//在0.00001的误差范围内进行比较
bool compare(float valueA,float  valueB){

    if(valueA>(valueB-0.00001) && valueA<(valueB+0.00001)){
    	return true;
    }else{
    	return false;
    }

}

void main()	{

    //屏幕坐标系变换到NDC标准设备空间
    vec4 ndcPosition = viewPortMatrixInverse * vec4(gl_FragCoord.xyz,1.0);

    //标准设备空间变换到剪裁空间
    vec4 clipPosition = ndcPosition/gl_FragCoord.w;

    //剪裁空间变换到相机空间
    vec4 viewPosition = projectionMatrixInverse * clipPosition;
    
    vec4 modelPosition = viewMatrixInverse * viewPosition;

    //vs传递过来相机空间的顶底坐标和通过gl_FragCoord算出的相机空间进行比对
    if(	compare(modelPosition.x,mPosition.x)&&
        compare(modelPosition.y,mPosition.y)&&
        compare(modelPosition.z,mPosition.z)&& testFlag){

        //黄色，计算正确显示
        gl_FragColor=vec4(1.0,1.0,0.0,1.0);

    }else{

        //红色，默认显示
        gl_FragColor=vec4(1.0,0.0,0.0,1.0);
    }

}
```

viewPortMatrixInverse 获取代码

```javascript
function getViewPortMatrixInverse(viewPort, depthRange) {

    var _viewPortMatrix = new THREE.Matrix4();
    var _viewPortMatrixInverse = new THREE.Matrix4();

    _viewPortMatrix.set(
    viewPort.width / 2, 0, 0, viewPort.x + viewPort.width / 2,
    0, viewPort.height / 2, 0, viewPort.y + viewPort.height / 2,
    0, 0, (depthRange.far - depthRange.near) / 2, (depthRange.far + depthRange.near) / 2,
    0, 0, 0, 1
    );

    _viewPortMatrixInverse.getInverse(_viewPortMatrix, true);

    return _viewPortMatrixInverse;
}
```

[参考：gl_FragCoord的含义](https://blog.csdn.net/fatcat123/article/details/103861403/?utm_medium=distribute.pc_relevant.none-task-blog-baidujs_title-0&spm=1001.2101.3001.4242)